@Metadata.allowExtensions: true
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'Proj. view for Travel Analytical Table'
@Metadata.ignorePropagatedAnnotations: true
@OData.applySupportedForAggregation: #FULL

define root view entity ZC_TRAVEL_ANAJU2
  provider contract transactional_query
  as projection on ZR_TRAVELJU2
  association [1..1] to ZR_TRAVELJU2 as _BaseEntity on $projection.UUID = _BaseEntity.UUID
{
  key UUID,
      TravelID,
      AgencyID,
      CustomerID,
      BeginDate,
      EndDate,

      @Aggregation.default: #AVG
      @EndUserText.label: 'Booking Fee (#AVG)'
      @Semantics.amount.currencyCode: 'CurrencyCode'
      BookingFee,

      @Aggregation.default: #MIN
      @EndUserText.label: 'Min Booking Fee (#MIN)'
      @Semantics.amount.currencyCode: 'CurrencyCode'
      BookingFee as MinBookingFee,

      @Aggregation.default: #MAX
      @EndUserText.label: 'Max Booking Fee (#MAX)'
      @Semantics.amount.currencyCode: 'CurrencyCode'
      BookingFee as MaxBookingFee,

      @Aggregation.default: #SUM
      @EndUserText.label: 'Total Price (#SUM)'
      @Semantics.amount.currencyCode: 'CurrencyCode'
      TotalPrice,

      @Consumption: { valueHelpDefinition: [{ entity.element: 'Currency', entity.name: 'I_CurrencyStdVH',useForValidation: true }] }
      CurrencyCode,
      Description,
      Notification,
      Status,

      /* public associations*/
      _Booking : redirected to composition child ZC_BOOKING_ANAJU2,
      _BaseEntity
}
